: ( 41 WORD ; IMMEDIATE
: \ 10 WORD ; IMMEDIATE

(
**************************
* Forth CORE Definitions *
**************************
)

\ Dictionary and Vocabulary
: CELLSIZE 4 ;
: CELLS CELLSIZE * ;
: ALLOT DP @ + DP ! ;
: LATEST CURRENT @ @ ;
: NFA>CFA 3 CELLS + ;
: NFA>PFA NFA>CFA CFA>PFA ;
: CFA>LFA CELLSIZE - ;
: CFA>NFA 3 CELLS - ;
: PFA>NFA 4 CELLS - ;
: NFA>LFA 2 CELLS + ;
: DEFINITIONS CONTEXT @ CURRENT ! ;
: RECURSE LATEST NFA>CFA , ; IMMEDIATE

\ Number base conversion
: BINARY 2 BASE ! ;
: OCTAL 8 BASE ! ;
: DECIMAL 10 BASE ! ;
: HEX 16 BASE ! ;

\ Conditional branching
: IF COMPILE 0BRANCH HERE 0 , ; IMMEDIATE
: ENDIF HERE SWAP ! ; IMMEDIATE
: ELSE COMPILE BRANCH HERE SWAP 0 , HERE SWAP ! ; IMMEDIATE

\ Iteration
: BEGIN HERE ; IMMEDIATE
: UNTIL COMPILE 0BRANCH , ; IMMEDIATE
: WHILE COMPILE 0BRANCH HERE 0 , ; IMMEDIATE
: REPEAT COMPILE BRANCH SWAP , HERE SWAP ! ; IMMEDIATE
: DO COMPILE (DO) HERE ; IMMEDIATE
: LOOP COMPILE (LOOP) , ; IMMEDIATE
: +LOOP COMPILE (+LOOP) , ; IMMEDIATE
: EXIT R> DROP ;

\ Compiler
: LITERAL STATE @ IF COMPILE LIT , ENDIF ; IMMEDIATE
: COMPILE-LITERAL COMPILE LIT , ;
: CONSTANT <BUILDS , DOES> @ ;
: VARIABLE CREATE 0 , ;
: TIMER CREATE 0 , 0 , ;

\ String and console output
32 CONSTANT BL \ Blank
10 CONSTANT LF \ Linefeed
: SPACE BL EMIT ;
: CR LF EMIT ;
: TAB 9 EMIT ;

: "SIZE ( STRING address - Size of a CELL aligned STRING )
	"COUNT 1+ CELLSIZE + CELLSIZE / CELLS
;

: (.") R@ TYPE R> DUP "SIZE + >R ;

: ."
	34 WORD PAD
	STATE @
	IF
		COMPILE (.")
		HERE "MOVE
		HERE "SIZE ALLOT
	ELSE
		TYPE
	ENDIF
; IMMEDIATE

: (S") R@ R> DUP "SIZE + >R ;

: S"
	34 WORD PAD
	STATE @
	IF
		COMPILE (S")
		HERE "MOVE
		HERE "SIZE ALLOT
	ENDIF
; IMMEDIATE

\ Compiler
: [COMPILE]  ( Compiles an IMMEDIATE word. )
	STATE @
	IF -FIND DUP
		IF NFA>CFA ,
		ELSE
			PAD UNDEFINED
		ENDIF
	ENDIF
; IMMEDIATE

: ' -FIND
	DUP
	IF
		NFA>CFA
	ELSE
		PAD UNDEFINED
	ENDIF
;

: ['] ' COMPILE LIT , ; IMMEDIATE

: DEFER <BUILDS ['] ABORT , DOES> @ EXECUTE ;
: (IS) CFA>PFA ! ;
: IS
	STATE @
	IF
		[COMPILE] ['] COMPILE (IS)
	ELSE
		' (IS)
	ENDIF
; IMMEDIATE

\ Magnitude
: MAX 2DUP < IF SWAP ENDIF DROP ;
: MIN 2DUP > IF SWAP ENDIF DROP ;
: NEGATE 0 SWAP - ;
: ABS DUP 0< IF NEGATE ENDIF ;

\ Misc

: [] ( #cells -- )
	<BUILDS CELLS ALLOT
	DOES> ( index -- addr )
	SWAP CELLS +
;

: (ABORT") IF R@ ERROR ELSE R> DUP "SIZE + >R ENDIF ;

: ABORT" ( b -- )
	34 WORD PAD
	STATE @
	IF
		COMPILE (ABORT")
		HERE "MOVE
		HERE "SIZE ALLOT
	ELSE
		SWAP
		IF
			TYPE CR ABORT
		ELSE
			DROP
		ENDIF
	ENDIF
; IMMEDIATE

: ASCII ( -- c )
	BL WORD PAD C@
	STATE @
	IF
		[COMPILE] LITERAL
	ENDIF
; IMMEDIATE

: ERASE ( addr len -- ) 0 FILL ;

: (FORGET) ( cfa -- )
	DUP CFA>NFA @ DP ! \ Reset dictionary pointer
	CFA>LFA @ CURRENT @ ! CURRENT @ CONTEXT ! \ Reset vocabulary links
;
: FORGET ' (FORGET) ;

\ Convenience
: ? @ . ;
: .S
	DEPTH 0<>
	IF
		DEPTH
		BEGIN
			DUP PICK . 1- DUP 0=
		UNTIL
		DROP
	ENDIF
;

: ON ( addr -- ) TRUE SWAP ! ;
: OFF ( addr -- ) FALSE SWAP ! ;

: THEN [COMPILE] ENDIF ; IMMEDIATE

: RANDOMIZE SYSTEM-TIME SEED! ;
: RANDOMIZE2 SYSTEM-TIME SEED2! ;

\ Floating point
: FVARIABLE [COMPILE] VARIABLE ;
: FCONSTANT [COMPILE] CONSTANT ;
: FCELLS [COMPILE] CELLS ;
: F, [COMPILE] , ;
: FMIN F2DUP F< IF DROP ELSE SWAP DROP ENDIF ;
: FMAX F2DUP F> IF DROP ELSE SWAP DROP ENDIF ;
: FNEGATE 0. FSWAP F- ;
: FABS FDUP F0< IF FNEGATE ENDIF ;

\ Message box
0  CONSTANT #MB_OK
1  CONSTANT #MB_OKCANCEL
4  CONSTANT #MB_YESNO
3  CONSTANT #MB_YESNOCANCEL
5  CONSTANT #MB_RETRYCANCEL
2  CONSTANT #MB_ABORTRETRYIGNORE

64 CONSTANT #MB_ICONINFORMATION
48 CONSTANT #MB_ICONEXCLAMATION
32 CONSTANT #MB_ICONQUESTION
16 CONSTANT #MB_ICONSTOP

3  CONSTANT #IDABORT
2  CONSTANT #IDCANCEL
11 CONSTANT #IDCONTINUE
5  CONSTANT #IDIGNORE
7  CONSTANT #IDNO
1  CONSTANT #IDOK
4  CONSTANT #IDRETRY
10 CONSTANT #IDTRYAGAIN
6  CONSTANT #IDYES
